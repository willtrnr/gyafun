;; Try implementing a basic cat

main:

  $argc 0
  $argv 1
  $idx 2

  ;; Check if we have enough args
  LDL $argc
  .min_args: 2
  LDC .min_args
  ILT .show_usage

  ;; Loop args
  ST1 $idx
  .loop:
    LDL $idx
    LDL $argc
    IEQ .end

    ;; Get the file name
    LDL $argv
    LDL $idx
    ALD

    ;; Read the file
    IVK read_file 1
    POP

    ;; Increment index
    LDL $idx
    INC 1
    STL $idx

    JMP .loop
  .end:

  LD0
  RET

  .show_usage:
    .usage: "Usage: %s <FILE> [FILE [FILE [...]]]\n"
    LDC .usage
    LDL $argv
    LD0
    ALD
    IVK io.printf 2
    POP
    LD1
    RET

read_file:

  $path 0
  $file 1

  ;; Open the file for reading
  LDL $path
  .mode: "r"
  LDC .mode
  IVK io.fopen 2
  DUP
  STL $file

  ;; If not open goto error
  IIN .error_not_open

  ;; It was indeed open, loop and print
  .loop:
    ;; Read chunk from file
    LDL $file
    .chunk_size: 1024
    LDC .chunk_size
    IVK io.fread 2
    DUP

    ;; Check if data was read
    IIN .end

    ;; Print chunk
    IVK io.print 1
    POP

    JMP .loop
  .end:

  POP

  ;; Close file
  LDL $file
  IVK io.fclose 1
  RET

  .error_not_open:
    .open_error: "%s: File not found or not readable\n"
    LDC .open_error
    LDL $path
    IVK io.printf 2
    RET
